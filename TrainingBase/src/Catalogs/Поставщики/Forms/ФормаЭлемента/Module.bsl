
&НаКлиенте
Асинх Процедура ЗаполнитьПоИНН(Команда)
	
	Если ЗначениеЗаполнено(Объект.Наименование) Или ЗначениеЗаполнено(Объект.НаименованиеПолное) Тогда
		Ответ = Ждать ВопросАсинх("Данные по поставщику уже заполнены. Перезаполнить?", РежимДиалогаВопрос.ДаНет);
		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	АдресAPI = "http://www.portal.nalog.gov.by/grp/getData";
	ИНН = Объект.ИНН;
	Кодировка = "UTF-8";
	ФорматОтвета = "json";
	ПолныйАдресAPI = СтрШаблон("%1?unp=%2&charset=%3&type=%4", АдресAPI, ИНН, Кодировка, ФорматОтвета);
	ВыполнитьHTTPЗапрос(ПолныйАдресAPI);

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьHTTPЗапрос(ПолныйАдресРесурса)
	СтруктураURI = СтруктураURI(ПолныйАдресРесурса); 
	HTTPСоединение = Новый HTTPСоединение(СтруктураURI.Хост, СтруктураURI.Порт); 
	 
	HTTPЗапрос = Новый HTTPЗапрос(СтруктураURI.ПутьНаСервере); 
	Попытка
		Результат =  HTTPСоединение.Получить(HTTPЗапрос);
	Исключение
		 // исключение здесь говорит о том, что запрос не дошел до HTTP-Сервера
		 Сообщить("Произошла сетевая ошибка!");
		 ВызватьИсключение;
	 КонецПопытки;
	 	 
	 Чтение = Новый ЧтениеJSON;
	 Чтение.ОткрытьПоток(Результат.ПолучитьТелоКакПоток());
	 Данные = ПрочитатьJSON(Чтение);
	 
	 Если ТипЗнч(Данные) = Тип("Структура") Тогда
		 Если Данные.Свойство("ROW") И ТипЗнч(Данные.ROW) = Тип("Структура") Тогда
			 Если Данные.ROW.Свойство("VNAIMK") Тогда
				 Объект.Наименование = Данные.ROW.VNAIMK;
			 КонецЕсли;
			 
			 Если Данные.ROW.Свойство("VNAIMP") Тогда
				 Объект.НаименованиеПолное = Данные.ROW.VNAIMP;
			 КонецЕсли;
		 КонецЕсли;
	 КонецЕсли;
	 
 КонецПроцедуры

&НаКлиенте
Функция СтруктураURI(Знач СтрокаURI)
	
	СтрокаURI = СокрЛП(СтрокаURI);
	
	// схема
	Схема = "";
	Позиция = Найти(СтрокаURI, "://");
	Если Позиция > 0 Тогда
		Схема = НРег(Лев(СтрокаURI, Позиция - 1));
		СтрокаURI = Сред(СтрокаURI, Позиция + 3);
	КонецЕсли;

	// строка соединения и путь на сервере
	СтрокаСоединения = СтрокаURI;
	ПутьНаСервере = "";
	Позиция = Найти(СтрокаСоединения, "/");
	Если Позиция > 0 Тогда
		ПутьНаСервере = Сред(СтрокаСоединения, Позиция + 1);
		СтрокаСоединения = Лев(СтрокаСоединения, Позиция - 1);
	КонецЕсли;
		
	// информация пользователя и имя сервера
	СтрокаАвторизации = "";
	ИмяСервера = СтрокаСоединения;
	Позиция = Найти(СтрокаСоединения, "@");
	Если Позиция > 0 Тогда
		СтрокаАвторизации = Лев(СтрокаСоединения, Позиция - 1);
		ИмяСервера = Сред(СтрокаСоединения, Позиция + 1);
	КонецЕсли;
	
	// логин и пароль
	Логин = СтрокаАвторизации;
	Пароль = "";
	Позиция = Найти(СтрокаАвторизации, ":");
	Если Позиция > 0 Тогда
		Логин = Лев(СтрокаАвторизации, Позиция - 1);
		Пароль = Сред(СтрокаАвторизации, Позиция + 1);
	КонецЕсли;
	
	// хост и порт
	Хост = ИмяСервера;
	Порт = "";
	Позиция = Найти(ИмяСервера, ":");
	Если Позиция > 0 Тогда
		Хост = Лев(ИмяСервера, Позиция - 1);
		Порт = Сред(ИмяСервера, Позиция + 1);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Схема", Схема);
	Результат.Вставить("Логин", Логин);
	Результат.Вставить("Пароль", Пароль);
	Результат.Вставить("ИмяСервера", ИмяСервера);
	Результат.Вставить("Хост", Хост);
	Результат.Вставить("Порт", ?(Порт <> "", Число(Порт), Неопределено));
	Результат.Вставить("ПутьНаСервере", ПутьНаСервере);
	
	Возврат Результат;
	
КонецФункции
